// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:

    // reset = 1 -> 11
    // load = 1 -> 10
    // inc = 1 -> 01
    // other -> 00
    Or (a=reset, b=load, out=resetOrLoad);
    Mux (a=inc, b=reset, sel=resetOrLoad, out=sel0);
    Inc16 (in=outReg, out=outRegIncremented);

    Mux4Way16 (a=outReg, b=outRegIncremented, c=in, d=false, sel[0]=sel0, sel[1]=resetOrLoad, out=inReg);
    
    Register (in=inReg, load=true, out=outReg);

    Or16 (a=outReg, b=outReg, out=out);
}
